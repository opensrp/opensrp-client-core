description = 'OpenSRP core client library'

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidToolsBuildGradle"
        classpath 'org.apache.commons:commons-lang3:3.12.0'
    }
}

allprojects {
    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        mavenLocal()
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven-publish'

jacoco {
    toolVersion = "0.8.8"
}

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultPublishConfig !isReleaseBuild() ? "debug" : "release"

    signingConfigs {
        config {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("$rootDir/assets/security/debug.keystore")
            storePassword 'android'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled = false
            signingConfig signingConfigs.config
            zipAlignEnabled true
            buildConfigField "String", "TRUST_MANAGER_ALGORITHM", "\"X509\""
            buildConfigField "String", "SSL_CONTEXT_PROTOCOL", "\"TLSv1.2\""
            buildConfigField "String", "OPENSRP_TRUSTORE_PASS", "\"phone red pen\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            buildConfigField "String", "TRUST_MANAGER_ALGORITHM", "\"X509\""
            buildConfigField "String", "SSL_CONTEXT_PROTOCOL", "\"TLSv1.2\""
            buildConfigField "String", "OPENSRP_TRUSTORE_PASS", "\"phone red pen\""
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        // Enabling multidex support.
        multiDexEnabled true
        //applicationId "org.smartregister"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "int", "DB_ENCRYPTION_VERSION", '1'
        buildConfigField("int", "VERSION_CODE", "${defaultConfig.versionCode}")
        buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDir 'src/main/java'
            res.srcDirs = ['res']
            assets.srcDir 'assets'
            jniLibs.srcDirs 'src/main/native'
            resources.srcDir 'resources'
        }
        androidTest.setRoot('src/androidTest')
    }

    aaptOptions {
        noCompress 'apk'
    }

    // avoid errors with message 'Duplicate files copied in APK ...'
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/*'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
    testLogging {
        // Enables easier debugging of tests in Github Actions CI
        events TestLogEvent.FAILED
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
    }
    maxHeapSize = "4g"
}

def filesToCopy = copySpec {
    from "${projectDir}/src/test/resources"
}

task copyTestResources() {
    def path = "${buildDir}/intermediates/classes/test"
    def paths = ["${path}/debug", "${path}/release"]
    paths.each { dest ->
        copy {
            with filesToCopy
            into dest
        }
    }
}

afterEvaluate {
    assembleDebug.dependsOn(copyTestResources)
}
configurations.all {
    resolutionStrategy.force 'commons-codec:commons-codec:1.15'
}

dependencies {

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.test:monitor:1.5.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
    implementation 'net.zetetic:android-database-sqlcipher:4.5.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.3'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-android'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation group: 'org.mozilla', name: 'rhino', version: '1.7.14'
    implementation 'org.ocpsoft.prettytime:prettytime:5.0.6.Final'
    api 'joda-time:joda-time:2.10.14'
    implementation('com.github.bmelnychuk:atv:1.2.9') {
        exclude group: 'com.google.android', module: 'android'
    }

    implementation('com.github.johnkil.print:print:1.3.1') {
        exclude group: 'com.google.android', module: 'android'
    }

    implementation('ch.acra:acra:4.11.1') {
        exclude group: 'org.json', module: 'json'
    }

    implementation 'com.github.ybq:Android-SpinKit:1.4.0'
    implementation 'com.github.bumptech.glide:glide:4.13.0'

    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.0'
    annotationProcessor 'com.jakewharton:butterknife:10.2.3'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.security:security-crypto:1.0.0'

    implementation('com.google.android.material:material:1.6.1') {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    implementation('com.evernote:android-job:1.4.3') {
        exclude group: 'com.google.android', module: 'android'
    }

    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'

    implementation('de.hdodenhof:circleimageview:3.1.0') {
        exclude group: 'com.google.android', module: 'android'
    }

    implementation('org.smartregister:android-p2p-sync:0.3.7-SNAPSHOT') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'android.arch.core', module: 'runtime'
    }

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    implementation 'org.smartregister:opensrp-client-utils:0.0.6-SNAPSHOT'

    implementation 'org.smartregister:opensrp-plan-evaluator:1.6.7-SNAPSHOT'

    implementation 'xerces:xercesImpl:2.12.2'

    compileOnly platform('com.google.firebase:firebase-bom:30.0.2')
    compileOnly 'com.google.firebase:firebase-crashlytics'
    compileOnly 'com.google.firebase:firebase-perf'

    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime:$work_version"

    // Add the dependency for the Performance Monitoring library

    //Mockito
    def mockitoVersion = '4.6.1'
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation("org.mockito:mockito-inline:$mockitoVersion")

    testImplementation "androidx.test:core:1.5.0-rc01"
    testImplementation "androidx.test.ext:junit:1.1.4-rc01"

    testAnnotationProcessor group: 'com.jakewharton', name: 'butterknife', version: '10.2.3'

    //Robolectric
    def robolectricVersion = '4.8.1'
    testImplementation(group: 'org.robolectric', name: 'robolectric', version: "$robolectricVersion") {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.apache.maven', module: 'maven-model'
        exclude group: 'com.android.support'
        exclude group: 'androidx.test'
        exclude group: 'com.thoughtworks.xstream', module: 'xstream'
    }

    testImplementation(group: 'org.robolectric', name: 'shadows-multidex', version: "$robolectricVersion") {
        exclude group: 'com.google.guava', module: 'guava'
    }

    testRuntimeOnly platform('com.google.firebase:firebase-bom:30.0.2')
    testRuntimeOnly 'com.google.firebase:firebase-crashlytics'
    // Add the dependency for the Performance Monitoring library
    testImplementation 'com.google.firebase:firebase-perf'

    // PowerMock
    def powerMockVersion = '2.0.9'
    testImplementation "org.powermock:powermock-core:$powerMockVersion"

    def fragmentVersion = "1.5.4"
    testImplementation "androidx.fragment:fragment-testing:$fragmentVersion"

    testImplementation 'org.skyscreamer:jsonassert:1.5.1'
}

task clearJar(type: Delete) {
    delete 'build/libs/myCompiledLibrary.jar'
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename('classes.jar', 'myCompiledLibrary.jar')
}

makeJar.dependsOn(clearJar, build)

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

    reports {
        xml.required.set(true)
        html.required.set(true)
    }

    getReports().getXml().setDestination(file("${buildDir}/reports/jacoco/jacocoRootReport/merged.xml"))
    getReports().getHtml().setDestination(file("${buildDir}/reports/jacoco/jacocoRootReport/html"))

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$ViewBinder*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug/classes", excludes: fileFilter)
    def kotlinDebugTree = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree, kotlinDebugTree]))
    executionData.setFrom(fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ]))
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/merged.xml"
    sourceDirs = ["$project.projectDir/src/main/java"]
}

apply from: '../maven.gradle'
apply plugin: 'kotlin-android'
